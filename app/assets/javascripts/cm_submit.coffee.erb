window.CampaignMonitorSubmit = class CampaignMonitorSubmit
  constructor: ->
    $(document).ready =>
      @init()

  init: ->
    return if $('form[action*="email.yourdaytheirday.com"]').length < 1
    @attach form for form in $('form[action*="email.yourdaytheirday.com"]')

  addErrorHtml: (form) ->
    $(form).prepend('<p class="error-message" />')

  attach: (form) ->
    $(form).submit (e) =>
      e.preventDefault()

      $elem = $(e.currentTarget)
      url = $elem.attr('action')

      if $(form).find('input[type="email"]').val().length < 1
        @showError form, "<%= I18n.t('form.error_no_email') %>"
        return

      $elem.find('button').prop('disabled', true);
      $.getJSON "#{url}?callback=?", $elem.serialize(), (data) =>
        if data.Status is 400
          $elem.find('button').prop('disabled', false);
          @showError form, "<%= I18n.t('form.signup_error') %>"
        else # 200
          @showSuccess form, "<%= I18n.t('form.signup_success') %>"
          @sendTracking(form) if typeof(ga) isnt 'undefined'


  sendTracking: (form) ->
    label = $(form).data('ga-tracking-label') or 'campaign monitor sign-up'
    ga('send', 'event', 'form', 'submit', label);

  showError: (form, message) ->
    alert message
    $(form).find('[type="email"]').addClass('error')
    @addErrorHtml(form) if $(form).find('.error-message').length < 1
    $(form).find('.error-message').text message

  showSuccess: (form, message) ->
    $(form).wrapInner('<div class="success" />')
    $msg = $(form).find('.success')
    $msg.css
      minHeight: $(form).find("[type=\"email\"]").outerHeight()

    $msg.animate
      opacity: 0,
    , =>
      $msg.html("<p>#{message}</p>")
      $msg.animate
        opacity: 1

new CampaignMonitorSubmit()
